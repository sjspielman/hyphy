/********************************************************************************************************************/
/********************************************* MODEL DEFINITIONS ****************************************************/
/********************************************************************************************************************/

/**
 * @name models.protein.Baseline.ModelDescription.withGamma
 * @description Define baseline (standard matrix) model w/ +F and *no* four-category gamma rate variation
 */
function pogofit.Baseline.ModelDescription(type){
    def = Call( models.protein.empirical.plusF_generators[pogofit.baseline_model], type);
    return def;
}
/**
 * @name models.protein.Baseline.ModelDescription.withGamma
 * @description Define baseline (standard matrix) model w/ +F and *yes* four-category gamma rate variation
 */
function pogofit.Baseline.ModelDescription.withGamma(type){
    def = pogofit.Baseline.ModelDescription(type);
	def [utility.getGlobalValue("terms.model.rate_variation")] = rate_variation.types.Gamma.factory ({utility.getGlobalValue("terms.rate_variation.bins") : 4});
    return def;
}


/**
 * @name pogofit.REV.ModelDescription.freqs
 * @description Define REV model frequencies as empirical
 */
function pogofit.REV.ModelDescription.freqs (model, namespace, datafilter) {
    model[terms.efv_estimate] = pogofit.shared_EFV;
    model[terms.model.efv_estimate_name] = terms.frequencies.predefined;
    (model[terms.parameters])[terms.model.empirical] = 0;
    return model;
}

/**
 * @name pogofit.REV.ModelDescription
 * @description Define a REV model with constant site rates
 */
function pogofit.REV.ModelDescription (type) {
    def = models.protein.REV.ModelDescription(type);
    if (Type (pogofit.shared_EFV) == "Matrix") {
        def [terms.model.frequency_estimator] = "pogofit.REV.ModelDescription.freqs";
    }
    return def;
}

/**
 * @name pogofit.REV.ModelDescription.withGamma
 * @description Define a REV model with Gamma rate variation
 */
function pogofit.REV.ModelDescription.withGamma (type) {
    def = pogofit.REV.ModelDescription(type);
    def [utility.getGlobalValue("terms.model.rate_variation")] = rate_variation.types.Gamma.factory ({utility.getGlobalValue("terms.rate_variation.bins"): 4});
    return def;
}
/********************************************************************************************************************/
/********************************************************************************************************************/
/********************************************************************************************************************/



/********************************************************************************************************************/
/********************************************** FITTING FUNCTIONS ***************************************************/
/********************************************************************************************************************/

/**
 * @name pogofit.fitBaselineToFile
 * @description Fits an empirical amino acid model to dataset for branch length optimization
 * @param {String} filename - The name of the file containing the dataset to which the amino acid model will be fitted
 * @return the fitted MLE
 */
function pogofit.fitBaselineToFile (filename) {


    //utility.EnsureKey(pogofit.analysis_results[pogofit.baseline_fit], pogofit.index_to_filename[filename]);

    pogofit.file_info = alignments.ReadNucleotideDataSet ("pogofit.msa",
                                                              filename);
    pogofit.name_mapping = pogofit.file_info[utility.getGlobalValue("terms.data.name_mapping")];
    if (None == pogofit.name_mapping) { /** create a 1-1 mapping if nothing was done */
        pogofit.name_mapping = {};
        utility.ForEach (alignments.GetSequenceNames ("pogofit.msa"), "_value_", "`&pogofit.name_mapping`[_value_] = _value_");
    }
    utility.ToggleEnvVariable ("GLOBAL_FPRINTF_REDIRECT", "/dev/null");
    ExecuteCommands ('pogofit.partitions_and_trees = trees.LoadAnnotatedTreeTopology.match_partitions (pogofit.file_info[terms.data.partitions], pogofit.name_mapping)',
                     {"0" : "Y"});
    utility.ToggleEnvVariable ("GLOBAL_FPRINTF_REDIRECT", None);



    pogofit.partition_count      = Abs (pogofit.partitions_and_trees);
    io.CheckAssertion ("pogofit.partition_count==1", "This analysis can only handle a single partition");



    pogofit.filter_specification = alignments.DefineFiltersForPartitions (pogofit.partitions_and_trees,
                                                                            "pogofit.msa" ,
                                                                            "pogofit.filter.",
                                                                            pogofit.file_info);


    pogofit.full_trees = utility.Map (pogofit.partitions_and_trees, "_value_", '_value_[terms.data.tree]');
    pogofit.full_data_filter = utility.Map (pogofit.filter_specification, "_value_", "_value_[terms.data.name]");

    utility.SetEnvVariable      ("VERBOSITY_LEVEL", 0);


    pogofit.baseline_mle = estimators.FitSingleModel_Ext(pogofit.full_data_filter,
                                                        pogofit.full_trees,
                                                        pogofit.baseline_model_desc,
                                                        None,
                                                        None);
                                                        //


    pogofit.baseline_mle - terms.global; // delete empty key

    return pogofit.baseline_mle;
}



/**
 * @name pogofit.handle_baseline_callback
 * @param node - node name which processed the given data
 * @param {Dict} result - Dictionary of fitted information for given data
 * @param {Dict} arguments - Dictionary with single key:value :: 0:datafile name
 * @description Handle MPI callback after fitting a baseline amino acid model (for initial branch length optimization)
 */
function pogofit.handle_baseline_callback (node, result, arguments) {

    savekey = pogofit.index_to_filename[arguments[0]];

    utility.EnsureKey(pogofit.analysis_results, pogofit.baseline_phase);
    utility.EnsureKey(pogofit.analysis_results[pogofit.baseline_phase], savekey);
    (pogofit.analysis_results[pogofit.baseline_phase])[savekey] = result;

    io.ReportProgressMessageMD ("Protein GTR Fitter", "Initial branch length fit",
                                "Received file '" + arguments[0] + "' from node " + node + ". LogL = " + result[terms.fit.log_likelihood]);
}



function pogofit.rms_error (log_l, params) {
    if (Type (pogofit.rms_error.last) != "Matrix") {
        pogofit.rms_error.last = {20,20};
    }
    t = 1;
    pogofit.lf.model.rv_gamma = 1;
    pogofit.rms_error.diff =  (pogofit.lf.model_Q - pogofit.rms_error.last)$({20,20}["_MATRIX_ELEMENT_ROW_>_MATRIX_ELEMENT_COLUMN_"]);
    pogofit.rms_error.diff = pogofit.rms_error.diff $ pogofit.rms_error.diff;
    pogofit.rms_error.diff = Sqrt ((+pogofit.rms_error.diff) / 190);
    pogofit.rms_error.last = pogofit.lf.model_Q;
    fprintf (stdout, "\n====>", pogofit.rms_error.diff, " / ", log_l, "\n");
    return pogofit.rms_error.diff;
}


function pogofit.fitGTR (current_results) {

    //file_list = utility.Keys (current_results); ---> pogofit.file_list
    //file_count = utility.Array1D (file_list);   ---> pogofit.file_list_count
    // NOTE: pogofit.index_to_filename is {filename:0, filename:1}

    partition_info = {};
    filter_info    = {};
    trees = {};
    initial_values = {terms.global : {},
                      terms.branch_length : {}};
    
    for (l1 = 0; l1 < 20; l1 += 1) {
        for (l2 = l1 + 1; l2 < 20; l2 += 1) {
            (initial_values[terms.global]) [terms.aminoacidRate (models.protein.alphabet[l1],models.protein.alphabet[l2])] = {terms.fit.MLE : (pogofit.initial_rates[models.protein.alphabet[l1]])[models.protein.alphabet[l2]]};
        }
    }

    index_to_file_name   = {};

    pogofit.fit_options_simple = {terms.run_options.retain_lf_object : TRUE};

    pogofit.fit_options = {terms.run_options.proportional_branch_length_scaler:
                        {
                        },
                        terms.run_options.retain_lf_object : TRUE
    };

    for (file_index = 0; file_index < pogofit.file_list_count; file_index += 1) {

        pogofit.partition_scaler = "pogofit.partition_scaler_" + (file_index+1);
        parameters.DeclareGlobalWithRanges (pogofit.partition_scaler, 1, 0, 100);
        (pogofit.fit_options[terms.run_options.proportional_branch_length_scaler])[file_index] = pogofit.partition_scaler;
        file_path = pogofit.file_list [file_index];
        dataset_name = "pogofit.msa." + file_index;
        partition_info [file_index] = alignments.ReadNucleotideDataSet (dataset_name, file_path);
        partition_specification = { "0" : {terms.data.name : "all", terms.data.filter_string : "", term.data.tree : ((current_results[file_index])[terms.fit.trees])[0]}};


        filter_info [file_index] = (alignments.DefineFiltersForPartitions (partition_specification,
                                                                            dataset_name ,
                                                                            dataset_name,
                                                                            partition_info [file_index]))[0];
        trees [file_index] = {terms.trees.newick :  ((current_results[file_index])[terms.fit.trees])[0]};
        (initial_values[terms.branch_length])[file_index] = ((current_results[file_index])[terms.branch_length])[0];
    }

    /*
    pogofit.rev_mle = estimators.FitSingleModel_Ext (
                                        utility.Map (filter_info, "_value_", "_value_[terms.data.name]"),
                                        trees,
                                        pogofit.rev_model,
                                        initial_values,
                                        pogofit.fit_options
                                   );

    pogofit.fit_options - terms.run_options.proportional_branch_length_scaler;

    //utility.SetEnvVariable      ("OPTIMIZATION_PRECISION", 0.1);

    utility.ForEachPair ( pogofit.rev_mle [terms.global], "_name_", "_value_",
    '
        (( pogofit.rev_mle [terms.global])[_name_]) [terms.fix] = TRUE;
    ');


    pogofit.rev_mle = estimators.FitSingleModel_Ext (
                                        utility.Map (filter_info, "_value_", "_value_[terms.data.name]"),
                                        trees,
                                        pogofit.rev_model,
                                        pogofit.rev_mle,
                                        pogofit.fit_options
                                   );

    //utility.SetEnvVariable      ("OPTIMIZATION_PRECISION", 0.001);

    utility.ForEachPair ( pogofit.rev_mle [terms.global], "_name_", "_value_",
    '
        (( pogofit.rev_mle [terms.global])[_name_]) [terms.fix] = FALSE;
    ');



    pogofit.rev_mle = estimators.FitSingleModel_Ext (
                                        utility.Map (filter_info, "_value_", "_value_[terms.data.name]"),
                                        trees,
                                        pogofit.rev_model,
                                        pogofit.rev_mle,
                                        pogofit.fit_options
                                        ); */

    df = estimators.CreateLFObject ("pogofit.lf",
                                    utility.Map (filter_info, "_value_", "_value_[terms.data.name]"),
                                    trees,
                                    pogofit.rev_model,
                                    initial_values,
                                    pogofit.fit_options_simple, None);

    utility.SetEnvVariable      ("VERBOSITY_LEVEL", 0);
    utility.ToggleEnvVariable   ("AUTO_PARALLELIZE_OPTIMIZE", 1);
    utility.ToggleEnvVariable   ("OPTIMIZATION_METHOD", 0);
    utility.SetEnvVariable      ("OPTIMIZATION_PRECISION", pogofit.rmse_precision);
    //utility.SetEnvVariable      ("USE_LAST_RESULTS", TRUE);
    utility.SetEnvVariable      ("LF_CONVERGENCE_CRITERION", "pogofit.rms_error");


    Optimize(mles, pogofit.lf.likelihoodFunction);

    pogofit.model_id_to_object = {
        "pogofit.lf.model": pogofit.lf.user_model
    };


    
    pogofit.rev_mle = estimators.ExtractMLEs( "pogofit.lf.likelihoodFunction", pogofit.model_id_to_object);
    pogofit.rev_mle [utility.getGlobalValue("terms.fit.log_likelihood")] = mles[1][0]; // keep logl in json
    
    /*
    // Uncomment these lines if you'd like to save the NEXUS LF.
    lf_id = pogofit.rev.mle[terms.likelihood_function];
    Export(pogofit.finalphase_LF, ^lf_id);
    fprintf(pogofit.final_likelihood_function, pogofit.finalphase_LF);
    */
    pogofit.rev_mle - terms.likelihood_function;

    // Save the rev.mle into the analysis_results, and cache it.
    (^"pogofit.analysis_results")[pogofit.final_phase] = pogofit.rev_mle;

    console.log (""); // clear past the optimization progress line
    utility.SetEnvVariable ("VERBOSITY_LEVEL", None);
    utility.ToggleEnvVariable ("AUTO_PARALLELIZE_OPTIMIZE", None);
    utility.ToggleEnvVariable ("OPTIMIZATION_METHOD", None);
    //utility.SetEnvVariable      ("USE_LAST_RESULTS", None);


    // Trees as dictionary for compatibility with rest of the output.
    pogofit.rev_mle[terms.fit.trees] = utility.SwapKeysAndValues(utility.MatrixToDict(pogofit.rev_mle[terms.fit.trees]));

    return pogofit.rev_mle;

}
/********************************************************************************************************************/
/********************************************************************************************************************/
/********************************************************************************************************************/



/********************************************************************************************************************/
/********************************************** UTILITY FUNCTIONS ***************************************************/
/********************************************************************************************************************/

function pogofit.save_options() {
    pogofit.analysis_results[utility.getGlobalValue("terms.json.options")] = {utility.getGlobalValue("pogofit.options.frequency_type"): pogofit.frequency,
                                                                                  utility.getGlobalValue("pogofit.options.baseline_model"): pogofit.baseline_model,
                                                                                  utility.getGlobalValue("pogofit.options.rmse_precision"): pogofit.rmse_precision,
                                                                                  utility.getGlobalValue("pogofit.options.imputation"): pogofit.imputation};

    pogofit.analysis_results[utility.getGlobalValue("terms.json.input")] = {utility.getGlobalValue("terms.json.file"): pogofit.input_file,
                                                                            pogofit.options.number_of_datasets: pogofit.file_list_count,
                                                                            pogofit.options.dataset_information: {}};


    /* Temporarily, we save input file information here in a highly redundant fashion, but doesn't seem possible to do in MPI...? */
    for (file_index = 0; file_index < pogofit.file_list_count; file_index += 1) {

        filename = pogofit.file_list[file_index];

        pogofit.file_info = alignments.ReadNucleotideDataSet ("pogofit.msa",
                                                                      filename);
        pogofit.name_mapping = pogofit.file_info[utility.getGlobalValue("terms.data.name_mapping")];
        if (None == pogofit.name_mapping) { /** create a 1-1 mapping if nothing was done */
            pogofit.name_mapping = {};
            utility.ForEach (alignments.GetSequenceNames ("pogofit.msa"), "_value_", "`&pogofit.name_mapping`[_value_] = _value_");
        }
        utility.ToggleEnvVariable ("GLOBAL_FPRINTF_REDIRECT", "/dev/null");
        ExecuteCommands ('pogofit.partitions_and_trees = trees.LoadAnnotatedTreeTopology.match_partitions (pogofit.file_info[terms.data.partitions], pogofit.name_mapping)',
                          {"0" : "Y"});
        utility.ToggleEnvVariable ("GLOBAL_FPRINTF_REDIRECT", None);


        pogofit.filter_specification = alignments.DefineFiltersForPartitions (pogofit.partitions_and_trees,
                                                                                    "pogofit.msa" ,
                                                                                    "pogofit.filter.",
                                                                                    pogofit.file_info);
        pogofit.tree = utility.Map (pogofit.partitions_and_trees, "_value_", '_value_[terms.data.tree]');


        pogofit.output_data_info = { utility.getGlobalValue("terms.json.sequences"): pogofit.file_info[utility.getGlobalValue("terms.data.sequences")],
                                         utility.getGlobalValue("terms.json.sites"): pogofit.file_info[utility.getGlobalValue("terms.data.sites")],
                                         utility.getGlobalValue("terms.json.trees"): (pogofit.tree["0"])[utility.getGlobalValue("terms.trees.newick_with_lengths")],
                                         utility.getGlobalValue("terms.json.file"): filename,
                                         utility.getGlobalValue("terms.original_name"): {}
                                       };


        //In case there were no branch lengths
        if (  Abs( (pogofit.tree["0"])[utility.getGlobalValue("terms.branch_length")] ) == 0 ){
            pogofit.output_data_info[ utility.getGlobalValue("terms.json.trees") ] = (pogofit.full_trees["0"])[utility.getGlobalValue("terms.trees.newick")];
        }
        utility.ForEach (utility.Keys (pogofit.name_mapping), "branch_name",
                                 "utility.EnsureKey (pogofit.output_data_info[terms.original_name], branch_name)");

        utility.ForEach (utility.Keys (pogofit.name_mapping), "branch_name",
                                 "(pogofit.output_data_info[terms.original_name])[branch_name] = pogofit.name_mapping[branch_name]");


        ((pogofit.analysis_results[terms.json.input])[pogofit.options.dataset_information])[file_index] = pogofit.output_data_info;
    }
}


lfunction pogofit.startTimer(timers, key) {
    timers[key] = {
        utility.getGlobalValue("terms.timers.timer"): Time(1),
    };

}
lfunction pogofit.stopTimer(timers, key) {
    (timers[key])[utility.getGlobalValue("terms.timers.timer")] = Time(1) - (timers[key])[utility.getGlobalValue("terms.timers.timer")];
}







// From the fitted model results, create EFV array which can be used for custom model
function pogofit.extract_efv () {
    if (pogofit.frequency_type == pogofit.ml_freq){
        fitted_efv = {20, 1};
        for (i = 0; i < 20; i+=1)
        {
            efv_search = terms.characterFrequency(models.protein.alphabet[i]);
            fitted_efv[i] = ((pogofit.gtr_fit[terms.global])[efv_search])[terms.fit.MLE];

        }
        norm =  +fitted_efv;
        fitted_efv = fitted_efv * (1/norm);
    }
    if (pogofit.frequency_type == pogofit.emp_freq) {
        fitted_efv = (pogofit.gtr_fit[terms.efv_estimate])["VALUEINDEXORDER"][0];
    }
    //console.log((results[terms.efv_estimate])["VALUEINDEXORDER"][0]);
    //console.log(pogofit.fitted_efv);
    return fitted_efv
}




// From the fitted model results and create rate dictionary which can be used for .fitted_model
function pogofit.extract_rates() {

    rij = {};
    for (l1 = 0; l1 < 20 - 1; l1 += 1) {
        rij[models.protein.alphabet[l1]] = {};
        for (l2 = l1 + 1; l2 < 20; l2 += 1) {

            rate_search = terms.aminoacidRate(models.protein.alphabet[l1], models.protein.alphabet[l2]);
            (rij[models.protein.alphabet[l1]])[models.protein.alphabet[l2]] = ((pogofit.gtr_fit[terms.global])[rate_search])[terms.fit.MLE];
        }
    }
    return rij
}


// From the fitted model results and create rate dictionary which can be used for .fitted_model
function pogofit.extract_rates_imputation() {


    pogofit.thekeys =  utility.Keys(pogofit.site_counts);
    summation = 0.0;
    for (x = 0; x < utility.Array1D(pogofit.thekeys); x+=1)
    {
        this_key = pogofit.thekeys[x];
        summation += ((1. + pogofit.site_counts[this_key]) * pogofit.tree_lengths[this_key] );            
    }
    
    rij = {};
    for (l1 = 0; l1 < 20 - 1; l1 += 1) {
        rij[models.protein.alphabet[l1]] = {};
        for (l2 = l1 + 1; l2 < 20; l2 += 1) {
            rate_search = terms.aminoacidRate(models.protein.alphabet[l1], models.protein.alphabet[l2]);
            this_rate = ((pogofit.gtr_fit[terms.global])[rate_search])[terms.fit.MLE];
            if (this_rate == 0.0) 
            {
                efv_sum = pogofit.final_efv[l1] + pogofit.final_efv[l2];
                this_rate = 1./ ((pogofit.final_efv[l1] + pogofit.final_efv[l2]) * summation);
            }
            console.log(this_rate);
            console.log("!!!!!!!!!!!!!!!!!!!!!");
            (rij[models.protein.alphabet[l1]])[models.protein.alphabet[l2]] = this_rate;
        }
    }
    return rij
}



function pogofit.write_model_to_file() {

    pogofit.external_order   = {{"A", "R", "N", "D", "C", "Q", "E", "G", "H", "I", "L", "K", "M", "F", "P", "S", "T", "W", "Y", "V"}};
    pogofit.hyphy_order_dict = utility.MatrixToDict(models.protein.alphabet);

    if (pogofit.output_format == pogofit.output_hyphy)
    {
        pogofit.save_hyphy_model();
    }

    if (pogofit.output_format == pogofit.output_paml)
    {
        pogofit.save_paml_model();
    }
    if (pogofit.output_format == pogofit.output_raxml)
    {
        pogofit.save_raxml_model();
    }
    if (pogofit.output_format == pogofit.output_all)
    {
        pogofit.save_raxml_model();
        pogofit.save_paml_model();
        pogofit.save_hyphy_model();
    }
}


function pogofit.save_hyphy_model(){
    fprintf(pogofit.output_model_prefix + pogofit.hyphy_model_ext, CLEAR_FILE, "Rij = " + pogofit.final_rij + ";");
    fprintf(pogofit.output_model_prefix + pogofit.hyphy_model_ext, "\n\n\n");
    fprintf(pogofit.output_model_prefix + pogofit.hyphy_model_ext, "EFV = " + pogofit.final_efv + ";");
}

function pogofit.save_raxml_model(){

    pogofit.raxml_output = "";
    pogofit.raxml_file = pogofit.output_model_prefix + pogofit.raxml_model_ext;

    for (i = 0; i < 20; i +=1)
    {
        for (j = 0; j < 20; j += 1)
        {

            if (i == j)
            {
                pogofit.raxml_output += "0.0\n";
            }
            else
            {
                pogofit.aa1 = pogofit.external_order[i];
                pogofit.aa2 = pogofit.external_order[j];

                if (pogofit.aa1 < pogofit.aa2)
                {
                    pogofit.rate = (pogofit.final_rij[pogofit.aa1])[pogofit.aa2];
                }
                else
                {
                    pogofit.rate = (pogofit.final_rij[pogofit.aa2])[pogofit.aa1];
                }
                pogofit.raxml_output += pogofit.rate;
                pogofit.raxml_output += "\n";
            }
        }
    }
    for (i = 0; i < 20; i += 1)
    {
        pogofit.raxml_output += pogofit.final_efv[ pogofit.hyphy_order_dict[pogofit.external_order[i]] ];
        // strip hack
        if (i <= 18){
            pogofit.raxml_output += "\n";
        }
    }

    fprintf(pogofit.raxml_file, CLEAR_FILE, pogofit.raxml_output);
}




function pogofit.save_paml_model(){

    pogofit.paml_output = "";
    pogofit.paml_file = pogofit.output_model_prefix + pogofit.paml_model_ext;

    for (i = 1; i < 20; i +=1)
    {
        pogofit.row = "";
        for (j = 0; j < i; j += 1)
        {

            pogofit.aa1 = pogofit.external_order[i];
            pogofit.aa2 = pogofit.external_order[j];

            if (pogofit.aa1 < pogofit.aa2){
                pogofit.rate = (pogofit.final_rij[pogofit.aa1])[pogofit.aa2];
            }
            else{
                pogofit.rate = (pogofit.final_rij[pogofit.aa2])[pogofit.aa1];
            }

            pogofit.row += pogofit.rate;
            // strip hack
            if (j != (i-1)){
                pogofit.row += " ";
            }
        }

        pogofit.paml_output += pogofit.row + "\n";
    }

    pogofit.paml_output += "\n";
    for (i = 0; i < 20; i += 1)
    {
        pogofit.paml_output += pogofit.final_efv[ pogofit.hyphy_order_dict[pogofit.external_order[i]] ];
        // strip hack
        if (i <= 18){
            pogofit.paml_output += " ";
        }
    }
    fprintf(pogofit.paml_file, CLEAR_FILE, pogofit.paml_output);
}
/********************************************************************************************************************/
/********************************************************************************************************************/
/********************************************************************************************************************/




